buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
		classpath 'com.matthewprenger:CurseGradle:1.0-SNAPSHOT'
		classpath 'org.ajoberstar:grgit:1.4.1'
	}
}

plugins {
	id "org.ajoberstar.grgit" version "1.4.1"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply from: 'mcbuild.gradle'

import groovy.io.FileType

repositories {
	maven {
		url "http://dvs1.progwml6.com/files/maven"
	}
}

dependencies {
    release "com.hea3ven.tools.mappings:h3nt-mappings:${version_h3nt_mappings}"

    // compile project(':asmtweaks')
    compile "com.hea3ven.tools.asmtweaks:h3nt-asmtweaks:${version_asmtweaks}"
    release "com.hea3ven.tools.asmtweaks:h3nt-asmtweaks:${version_asmtweaks}"

	// compile project(':CommonUtils')
	deobfCompile "com.hea3ven.tools.commonutils:h3nt-commonutils:${version_commonutils}"
	release "com.hea3ven.tools.commonutils:h3nt-commonutils:${version_commonutils}"

	deobfCompile "mezz.jei:jei_$version_mc:$version_jei"

	testCompile "junit:junit:4.12"
}

task updateCommonCode << {
	updateCommonCodeFrom("base/BlockBuildingBricksBase.java", "COMMON BLOCK CODE")
	updateCommonCodeFrom("BlockMaterialBlock.java", "COMMON TILE CODE")
}

def updateCommonCodeFrom(String masterCodeFile, regionName) {
	def commonCode = new ArrayList()
	def found = false
	def blocksDir = new File(project.projectDir, "src/main/java/com/hea3ven/buildingbricks/core/block");
	new File(blocksDir, masterCodeFile).eachLine { line ->
		if (line.contains("//region " + regionName))
			found = true
		else if (line.contains("//endregion" + regionName))
			found = false
		else {
			if (found)
				commonCode.add(line)
		}
	}

	blocksDir.eachFileRecurse(FileType.FILES) { file ->
		def tmpFile = new File(file.toString() + "tmp")
		found = false
		tmpFile.withWriter { w ->
			file.eachLine { line ->
				if (line.contains("//region " + regionName)) {
					found = true
					w << line + "\n"
					commonCode.forEach { w << it + "\n" }
				} else {
					if (line.contains("//endregion" + regionName))
						found = false
					if (!found) {
						w << line + "\n"
					}
				}
			}
		}

		file.delete()
		tmpFile.renameTo(file)
	}
}
